plugins {
    id 'java-library'
    id 'com.github.sherter.google-java-format' version '0.9'
    id 'groovy'
    id 'org.unbroken-dome.test-sets' version '4.0.0'
}

group = 'org.ethelred'

repositories {
    mavenCentral()
}

testSets {
    ngTest
    spockTest
}

dependencies {
    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'com.google.guava:guava:31.0.1-jre'
    implementation 'org.codehaus.groovy:groovy-all:3.0.9'

    // Use TestNG framework, also requires calling test.useTestNG() below
    ngTestImplementation 'org.testng:testng:7.5'

    // https://mvnrepository.com/artifact/net.java.dev.jna/jna
    implementation group: 'net.java.dev.jna', name: 'jna', version: '5.6.0'

    ngTestImplementation group: 'com.github.npathai', name: 'hamcrest-optional', version: '2.0.0'

    spockTestImplementation 'org.spockframework:spock-core:2.1-groovy-3.0'
}

ngTest {
    // Use TestNG for unit tests
    useTestNG()
}

spockTest {
    useJUnitPlatform()
}

check.dependsOn(ngTest)
check.dependsOn(spockTest)

task installGitHooks(type:Exec) {
    onlyIf { !project.hasProperty("skip.hooks")}
    commandLine "hooks/setup.sh"
}
compileJava.dependsOn installGitHooks

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}