plugins {
    id 'java-library'
//    id 'com.github.sherter.google-java-format' version '0.9'
    id 'groovy'
    id 'maven-publish'
    id 'signing'
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}

group = 'org.ethelred'
version = '2.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.guava:guava:31.0.1-jre'
	// mandatory dependencies for using Spock
	implementation platform('org.apache.groovy:groovy-bom:4.0.20')
	implementation 'org.apache.groovy:groovy'
	testImplementation platform("org.spockframework:spock-bom:2.3-groovy-4.0")
	testImplementation "org.spockframework:spock-core"
	testImplementation('org.apache.groovy:groovy-nio')
    // https://mvnrepository.com/artifact/net.java.dev.jna/jna
    implementation group: 'net.java.dev.jna', name: 'jna', version: '5.6.0'

}

test {
    useJUnitPlatform()
}

javadoc {
	if(JavaVersion.current().isJava9Compatible()) {
		options.addBooleanOption('html5', true)
	}
}

task installGitHooks(type:Exec) {
    onlyIf { !project.hasProperty("skiphooks")}
    commandLine "hooks/setup.sh"
}
compileJava.dependsOn installGitHooks

java {
	withJavadocJar()
	withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)

            pom {
				name = 'Ethelred Util'
				description = 'Utility classes'
				url = 'https://github.com/edward3h/ethelred_util'
				licenses {
					license {
						name = 'GNU General Public License, Version 3'
						url = 'https://choosealicense.com/licenses/gpl-3.0/'
					}
				}
				developers {
					developer {
						name = 'Edward Harman'
						email = 'jaq@ethelred.org'
					}
				}
				scm {
					connection = "https://github.com/edward3h/ethelred_util.git"
					developerConnection = "git@github.com:edward3h/ethelred_util.git"
					url = "https://github.com/edward3h/ethelred_util"
				}
			}
        }
    }
}

signing {
	def signingKey = findProperty("signingKey")
	def signingPassword = findProperty("signingPassword")
	useInMemoryPgpKeys(signingKey, signingPassword)
	sign publishing.publications.maven
}

nexusPublishing {
    repositories {
        sonatype {  //only for users registered in Sonatype after 24 Feb 2021
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}
